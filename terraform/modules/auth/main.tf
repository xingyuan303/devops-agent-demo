resource "aws_cognito_user_pool" "main" {
  name = "${var.project_name}-users"

  username_attributes      = ["email"]
  auto_verified_attributes = ["email"]

  admin_create_user_config {
    allow_admin_create_user_only = true
  }

  password_policy {
    minimum_length    = 8
    require_lowercase = true
    require_numbers   = true
    require_symbols   = false
    require_uppercase = true
  }

  schema {
    name     = "email"
    required = true
    mutable  = true

    attribute_data_type = "String"
    string_attribute_constraints {
      min_length = 0
      max_length = 2048
    }
  }

  schema {
    name     = "name"
    required = false
    mutable  = true

    attribute_data_type = "String"
    string_attribute_constraints {
      min_length = 0
      max_length = 256
    }
  }
}

resource "aws_cognito_user_pool_domain" "main" {
  # NOTE: Cognito custom domains are globally unique across ALL AWS accounts.
  # If deploying to a personal account and this domain is already taken,
  # append your account ID: "${var.project_name}-auth-${data.aws_caller_identity.current.account_id}"
  domain       = "${var.project_name}-auth-062109"
  user_pool_id = aws_cognito_user_pool.main.id
}

data "aws_caller_identity" "current" {}

# Amazon Federate OIDC Identity Provider
resource "aws_cognito_identity_provider" "federate" {
  count         = var.federate_enabled ? 1 : 0
  user_pool_id  = aws_cognito_user_pool.main.id
  provider_name = "AmazonFederate"
  provider_type = "OIDC"

  provider_details = {
    client_id                = var.federate_client_id
    client_secret            = var.federate_client_secret
    oidc_issuer              = var.federate_issuer_url
    authorize_scopes         = "openid email profile"
    attributes_request_method = "GET"
  }

  attribute_mapping = {
    email    = "email"
    name     = "name"
    username = "sub"
  }
}

locals {
  identity_providers = var.federate_enabled ? ["COGNITO", "AmazonFederate"] : ["COGNITO"]
}

resource "aws_cognito_user_pool_client" "outline" {
  name         = "outline"
  user_pool_id = aws_cognito_user_pool.main.id

  generate_secret                      = true
  allowed_oauth_flows_user_pool_client = true
  allowed_oauth_flows                  = ["code"]
  allowed_oauth_scopes                 = ["openid", "profile", "email"]
  supported_identity_providers         = local.identity_providers
  callback_urls                        = ["${var.outline_url}/auth/oidc.callback"]
  logout_urls                          = [var.outline_url]

  explicit_auth_flows = [
    "ALLOW_USER_SRP_AUTH",
    "ALLOW_REFRESH_TOKEN_AUTH",
  ]

  depends_on = [aws_cognito_identity_provider.federate]
}

resource "aws_cognito_user_pool_client" "grafana" {
  name         = "grafana"
  user_pool_id = aws_cognito_user_pool.main.id

  generate_secret                      = true
  allowed_oauth_flows_user_pool_client = true
  allowed_oauth_flows                  = ["code"]
  allowed_oauth_scopes                 = ["openid", "profile", "email"]
  supported_identity_providers         = local.identity_providers
  callback_urls                        = ["${var.grafana_url}/login/generic_oauth"]
  logout_urls                          = [var.grafana_url]

  explicit_auth_flows = [
    "ALLOW_USER_SRP_AUTH",
    "ALLOW_REFRESH_TOKEN_AUTH",
  ]

  depends_on = [aws_cognito_identity_provider.federate]
}
